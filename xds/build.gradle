plugins {
    id "java"
    id "maven-publish"

    id "com.github.johnrengelman.shadow"
    id "com.google.protobuf"
    id "ru.vyarus.animalsniffer"
}

description = "gRPC: XDS plugin"

[compileJava].each() {
    it.options.compilerArgs += [
            // valueOf(int) in RoutingPriority has been deprecated
            "-Xlint:-deprecation",
    ]
}

evaluationDependsOn(project(':grpc-core').path)

dependencies {
    compile project(':grpc-protobuf'),
            project(':grpc-stub'),
            project(':grpc-core'),
            project(':grpc-services'),
            project(path: ':grpc-alts', configuration: 'shadow')
    def nettyDependency = compile project(':grpc-netty')
    def pgvDependency = compile(libraries.pgv) {
        // PGV depends on com.google.protobuf:protobuf-java 3.6.1 conflicting with :grpc-protobuf
        exclude group: 'com.google.protobuf'
    }

    compile(libraries.protobuf_util) {
        // prefer our own versions instead of protobuf-util's dependency
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
    }

    testCompile project(':grpc-core').sourceSets.test.output

    compileOnly libraries.javax_annotation,
            // At runtime use the epoll included in grpc-netty-shaded
            libraries.netty_epoll

    testCompile project(':grpc-testing'),
            project(':grpc-testing-proto'),
            libraries.guava_testlib,
            libraries.netty_epoll

    shadow configurations.compile.getDependencies().minus([nettyDependency, pgvDependency])
    shadow project(path: ':grpc-netty-shaded', configuration: 'shadow')

    signature "org.codehaus.mojo.signature:java17:1.0@signature"
    testRuntime libraries.netty_tcnative
}

sourceSets {
    main {
        proto {
            srcDir 'third_party/envoy/src/main/proto'
            srcDir 'third_party/udpa/src/main/proto'
        }
    }
}

configureProtoCompilation()

jar {
    classifier = 'original'
}

javadoc {
    exclude 'io/grpc/xds/internal/**'
}

shadowJar {
    classifier = null
    dependencies {
        include(project(':grpc-xds'))
        include(dependency('io.envoyproxy.protoc-gen-validate:'))
    }
    relocate 'com.github.udpa', 'io.grpc.xds.shaded.com.github.udpa'
    relocate 'io.envoyproxy', 'io.grpc.xds.shaded.io.envoyproxy'
    relocate 'io.grpc.netty', 'io.grpc.netty.shaded.io.grpc.netty'
    relocate 'io.netty', 'io.grpc.netty.shaded.io.netty'
    exclude "**/*.proto"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact shadowJar
            artifacts.removeAll { it.classifier == 'original' }

            pom.withXml {
                def dependenciesNode = new Node(null, 'dependencies')
                project.configurations.shadow.allDependencies.each { dep ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep.group)
                    dependencyNode.appendNode('artifactId', dep.name)
                    def version = (dep.name == 'grpc-netty-shaded') ? '[' + dep.version + ']' : dep.version
                    dependencyNode.appendNode('version', version)
                    dependencyNode.appendNode('scope', 'compile')
                }
                asNode().dependencies[0].replaceNode(dependenciesNode)
            }
        }
    }
}
